package drools.recommendation;

import drools.recommendation.Recipe;
import drools.recommendation.RecipeIngredient;
import drools.recommendation.Recommendation;
import drools.recommendation.User;
import drools.recommendation.Ingredient;
import drools.recommendation.StorageItem;
import java.time.LocalDate;
import java.time.temporal.ChronoUnit;

import java.util.List;

rule "Recipes with ingredients which user likes"
    agenda-group "Recommendation"
    salience 100
    when
        $recipe: Recipe($id: id, $rIngredients: ingredients)
        $ingredients: List() from accumulate(
            RecipeIngredient($i: ingredient) from $rIngredients,
            collectList($i)
        )
        $user: User($likes: likes, $allergies: allergies, $unavailable: unavailable)
        Number($value: intValue >= 1) from accumulate(
                	Ingredient($t: this, this memberOf $ingredients) from $likes,
                	count($t)
        )
        not Ingredient(this memberOf $ingredients) from $allergies and
        not Ingredient(this memberOf $ingredients) from $unavailable
    then
        insert(new Recommendation($id, $value*10));
end

rule "Lower the possibility of recommending a recipe with ingredients which user dislikes"
    salience 99
    no-loop
    agenda-group "Recommendation"
    when
        $recommendation: Recommendation($id: recipeId, $hit: hit)
        $recipe: Recipe($rIngredients: ingredients)
        $ingredients: List() from accumulate(
            RecipeIngredient($i: ingredient) from $rIngredients,
            collectList($i)
        )
        $user: User($dislikes: dislikes)
        Number($value: intValue >= 1) from accumulate(
                Ingredient($t: this, this memberOf $ingredients) from $dislikes,
                count($t)
        )
    then
        $recommendation.setHit($hit - $value*20);
        update($recommendation);
end

rule "Recommend vegetarian recipes"
    agenda-group "Recommendation"
    when
        Vegetarian($userId: userId)
        Recipe($id: id, vegetarian == true)
    then
        insert(new Recommendation($id, 10));
        System.out.printf("Recommend vegetarian recipe %s to user %s.\n", $id, $userId);
end
